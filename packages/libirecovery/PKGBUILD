# Maintainer: 0x9fff00 <0x9fff00+git@protonmail.ch>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: Matthew Bauer <mjbauer95@gmail.com>

pkgname=libirecovery
pkgver=1.2.1
pkgrel=1
pkgdesc="Library and utility to talk to iBoot/iBSS via USB"
arch=('x86_64' 'armv7l' 'aarch64')
url="https://libimobiledevice.org/"
license=('LGPL-2.1-only')
depends=('libimobiledevice-glue' 'libusb' 'readline')
# systemd is needed to build udev rules and is not in base-devel
makedepends=('git' 'systemd')
source=("git+https://github.com/libimobiledevice/$pkgname.git#tag=$pkgver")
sha256sums=('3105d553392ef0b82cb1c8e1ebebe108c0aafccbd042a660612d0c7ab9f447de')

prepare() {
  cd "$pkgname"

  # don't overlink to libplist-2.0.so
  sed -Ei 's/(\$\(limd_glue_LIBS)\)/\1:-lplist-2.0=)/' src/Makefile.am
}

build() {
  cd "$pkgname"

  RELEASE_VERSION=$pkgver ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
}
